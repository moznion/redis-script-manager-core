// releng: gradle clean && gradle uploadArchives -Prelease

group 'net.moznion'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'war'

sourceCompatibility = 1.8

def isDevBuild
def isCiBuild
def isReleaseBuild
def sonatypeRepositoryUrl
if (hasProperty('release')) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
} else if (hasProperty('ci')) {
    isCiBuild = true
    version += '-SNAPSHOT'
    sonatypeRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
} else {
    isDevBuild = true
    version += '-SNAPSHOT'
}

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'redis-script-manager-core'
                    packaging 'jar'
                    description 'Simple manager for scripting of Redis'
                    url 'https://github.com/moznion/redis-script-manager-core'

                    scm {
                        url 'scm:git@github.com:moznion/redis-script-manager-core.git'
                        connection 'scm:git@github.com:moznion/redis-script-manager-core.git'
                        developerConnection 'scm:git@github.com:moznion/redis-script-manager-core.git'
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'moznion'
                            name 'Taiki Kawakami'
                            email 'moznion@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

